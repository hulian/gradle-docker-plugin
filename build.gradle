plugins{
    id 'com.gradle.plugin-publish' version '1.0.0' apply false
}

ext {
    set('lombokVersion', '1.18.18')
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'com.gradle.plugin-publish'
    apply plugin: 'java-gradle-plugin'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    group = 'pub.techfun'
    //version = '0.0.5-SNAPSHOT'
    version = '0.0.9'
    def isReleaseVersion = !version.endsWith("SNAPSHOT")

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    }

    task javadocJar(type: Jar) {
        archiveClassifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                group = group
                artifact jar
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = "gradle-docker-plugin"
                    description = "Gradle Docker打包插件"
                    url = "https://github.com/hulian/gradle-docker-plugin"
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    developers {
                        developer {
                            id = "hulian"
                            name = "hulian"
                            email = "hu_lian_cn@163.com"
                        }
                    }
                    scm {
                        connection = "scm:git:https://github.com/hulian/gradle-docker-plugin.git"
                        developerConnection = "scm:git: https://github.com/hulian/gradle-docker-plugin.git"
                        url = "https://github.com/hulian/gradle-docker-plugin"
                    }
                }
            }
        }

        repositories {
            maven {
                name = 'sonatype'
                if(isReleaseVersion){
                    url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                }else{
                    url = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                }
                credentials {
                    username = "$sonatypeUsername"
                    password = "$sonatypePassword"
                }
            }
        }
    }

    signing {
        required { isReleaseVersion && gradle.taskGraph.hasTask("publishMavenJavaPublicationToSonatypeRepository") }
        sign publishing.publications.mavenJava
    }
}
